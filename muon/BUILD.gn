dep_base = rebase_path("../..", "//")

config("external_config") {
  include_dirs = [ ".." ]
}

config("internal_config") {
  include_dirs = [
    rebase_path("node/src", dep_base),
  ]

  defines = [ "TP_MODULE_CONTEXT_AWARE" ]

  cflags = []

  if (is_clang) {
    cflags += [
      "-Wno-error",
      "-Wno-deprecated-declarations",
    ]
  }
}

source_set("tp_node_addon") {
  public_configs = [ ":external_config" ]

  configs += [ ":internal_config" ]

  sources = [
    "../TPParser.cpp",
    "../TPParser.h",
    "../TrackerData.h",
    "../FirstPartyHost.h",
    "../node_addon/addon.cpp",
    "../node_addon/TPParserWrap.cpp",
    "../node_addon/TPParserWrap.h",
  ]

  deps = [
    rebase_path("hashset-cpp", dep_base),
    "//v8",
  ]
}

